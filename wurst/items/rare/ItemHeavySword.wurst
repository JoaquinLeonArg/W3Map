package ItemHeavySword

import Constants
import ItemObjEditing
import ObjectIdGenerator
import ObjectIds
import AbilityObjEditing
import ItemTooltipGenerator
import ClosureEvents

@configurable constant GOLD_COST = 250
@configurable constant LUMBER_COST = 0
@configurable constant CURSED = false
@configurable constant EXTRA_DAMAGE = 50.
@configurable constant EXTRA_AS = -.4
@configurable constant EXTRA_CRIT_CHANCE = 40.
@configurable constant EXTRA_CRIT_DAMAGE = 4.
@configurable constant RARITY = Rarity.RARE

@compiletime function generateItemHeavySword()
    let tgen = new ItemTooltipGenerator("Heavy Sword", RARITY)
        ..addItemProperty("+" + EXTRA_DAMAGE.toInt().toString() + " attack damage")
        ..addItemProperty((EXTRA_AS*100).toInt().toString() + "% attack speed")
        ..addItemProperty("+" + (EXTRA_CRIT_CHANCE).toInt().toString() + "% critical attack chance")
        ..addItemProperty("+" + (EXTRA_CRIT_DAMAGE*100).toInt().toString() + "% critical attack damage")
        ..setShortDescription("Something something")

    new ItemDefinition(ITEM_HEAVYSWORD_ID, 'afac')
        ..setName(tgen.generateItemName())
        ..setTooltipExtended(tgen.generateItemDescription())
        ..setTooltipBasic(tgen.generateItemName())
        ..setDescription(tgen.generateItemDescription())
        ..setInterfaceIcon("ReplaceableTextures\\CommandButtons\\BTNTier6Sword.blp")
        ..setGoldCost(GOLD_COST)
        ..setLumberCost(LUMBER_COST)
        ..setLevel(GOLD_COST*GOLD_SHOP_OFFSET + LUMBER_COST)

init
    EventListener.add(EVENT_PLAYER_UNIT_PICKUP_ITEM) ->
        let u = GetManipulatingUnit()
        let i = GetManipulatedItem()
        if i.getTypeId() == ITEM_HEAVYSWORD_ID
            LinkBonusToItem(u, BONUS_DAMAGE, EXTRA_DAMAGE, i)
            LinkBonusToItem(u, BONUS_ATTACK_SPEED, EXTRA_AS, i)
            LinkBonusToItem(u, BONUS_CRITICAL_CHANCE, EXTRA_CRIT_CHANCE, i)
            LinkBonusToItem(u, BONUS_CRITICAL_DAMAGE, EXTRA_CRIT_DAMAGE, i)