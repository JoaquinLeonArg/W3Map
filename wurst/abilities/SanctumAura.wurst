package SanctumAura

import ChannelAbilityPreset
import Icons
import AbilityTooltipGenerator
import ClosureEvents
import ClosureForGroups
import ClosureTimers
import Effect
import Abilities
import DamageEvent
import Knockback3
import Registry

public constant ABILITY_SANCTUMAURA_ID = compiletime(ABIL_ID_GEN.next())

// Stats
@configurable constant BASE_MAX_HEALTH_PERC = 0.02
@configurable constant LEVEL_MAX_HEALTH_PERC = 0.01
@configurable constant RANGE = 300.
@configurable constant COOLDOWN = 15.

@compiletime function generateAbilitySanctumAura()
    let tgen = new AbilityTooltipGenerator("Restores life to surrounding allied units, equal to a percentage of their max life.")
    tgen.addProperty("Max life restored", lvl -> ((BASE_MAX_HEALTH_PERC + lvl * LEVEL_MAX_HEALTH_PERC)*100).toInt().toString() + "%")
    tgen.addConstantProperty("Cooldown", COOLDOWN.toInt().toString())
    tgen.addConstantProperty("Range", RANGE.toInt().toString())
    
    new AbilityDefinitionHardenedSkin(ABILITY_SANCTUMAURA_ID)
        ..registerTooltipGenerator(tgen)
        ..presetChancetoReduceDamage(lvl -> 0) // Disable hardened skin
        ..setRequirements("") // Disable hardened skin
        ..presetButtonPosNormal(2, 2)
        ..presetButtonPosResearch(2, 0)
        ..tooltipStartListen()
        ..setLevels(5)
        ..setName("Sanctum Aura")
        ..tooltipStopListen()
        ..presetIcon("ReplaceableTextures\\PassiveButtons\\PASHolyRestorationAura.blp")
        ..setLevelSkipRequirement(1)
        
public function registerUnitSanctumAura(unit u)
    Registry.registerUnitAbility(u, ABILITY_SANCTUMAURA_ID)

init
    doPeriodically(COOLDOWN) (CallbackPeriodic cb) ->
        for u in Registry.getUnitAbilityList(ABILITY_SANCTUMAURA_ID)
            if u.hasAbility(ABILITY_SANCTUMAURA_ID)
                let areaEffect = addEffect("Effects\\SoulArmorDivine", u.getPos())
                areaEffect.setScale(4.)
                areaEffect.setAlpha(20)
                doAfter(1) -> 
                    areaEffect.destr()
                let lvl = u.getAbilityLevel(ABILITY_SANCTUMAURA_ID)
                let perc_heal = BASE_MAX_HEALTH_PERC + lvl * LEVEL_MAX_HEALTH_PERC
                for t in GetUnitsInRangeOfLocAll(RANGE, u.getLoc())
                    t.addHP(t.getMaxHP() * perc_heal)