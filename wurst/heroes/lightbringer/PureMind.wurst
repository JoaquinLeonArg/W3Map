package PureMind

import AbilityTooltipGenerator
import ClosureTimers
import HashList
import OnUnitEnterLeave
import Constants

// Internals
HashList<unit> units = new HashList<unit>

// Stats
@configurable constant STR_PERC = .05

@compiletime function generateAbilityPureMind()
    let tgen = new AbilityTooltipGenerator("The hero recovers mana each second equal to a percentage of it's strength.")
    tgen.addConstantProperty("Amount", (STR_PERC*100).toInt().toString() + "%")
    
    new AbilityDefinitionHardenedSkin(ABILITY_PUREMIND_ID)
        ..registerTooltipGenerator(tgen)
        ..presetChancetoReduceDamage(lvl -> 0) // Disable hardened skin
        ..setRequirements("") // Disable hardened skin
        ..tooltipStartListen()
        ..setName("Pure Mind")
        ..tooltipStopListen()
        ..presetButtonPosNormal(1, 1)
        ..presetIcon("ReplaceableTextures\\CommandButtons\\BTNBlessedCircle.blp")

init
    // Unit registration
    onEnter() ->
        let u = getEnterLeaveUnit()
        if u.getTypeId() == HERO_LIGHTBRINGER_ID and not units.has(u)
            units.add(u)
    onLeave() ->
        let u = getEnterLeaveUnit()
        units.remove(u)

    // Apply mana regen each second.
    doPeriodically(1) (CallbackPeriodic cb) ->
        for u in units
            if u.hasAbility(ABILITY_PUREMIND_ID)
                let amount = u.getStr(true) * STR_PERC
                u.addMana(amount)

        